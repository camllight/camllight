\chapter{Lists}
\label{c:lists}

Lists represent an important data structure, mainly because of their success
in the Lisp language.
Lists in ML are {\it homogeneous\/}: a list cannot contain elements of
different types.
This may be annoying to new ML users, yet lists are not as fundamental
as in Lisp, since ML provides a facility for introducing new types allowing the
user to define more precisely the data structures needed by the
program (cf. chapter~\ref{c:udeftypes}).

\section{Building lists}

Lists are empty or non empty sequences of elements.
They are built with two {\it value constructors\/}:
\begin{itemize}
\item \verb"[]", the empty list (read: {\it nil\/});
\item \verb"::", the non-empty list constructor (read: {\it cons\/}). It takes an element $e$ and a list $l$,
and builds a new list whose first element ({\it head\/}) is $e$ and rest ({\it
tail\/}) is $l$.
\end{itemize}
The special syntax \verb"["$e_1$\verb";" \ldots\verb";" $e_n$ \verb"]"
builds the list whose elements are $e_1, \ldots, e_n$. It is
equivalent to $e_1$ \verb"::" $(e_2$ \verb"::" \ldots $(e_n$ \verb":: []"%
$)\ldots)$.


We may build lists of numbers:
\begin{caml_example}
1::2::[];;
[3;4;5];;
let x=2 in [1; 2; x+1; x+2];;
\end{caml_example}
Lists of functions:
\begin{caml_example}
let adds =
  let add x y = x+y
  in [add 1; add 2; add 3];;
\end{caml_example}
and indeed, lists of anything desired.


We may wonder what are the types of the list (data) constructors. The
empty list is a list of anything (since it has no element), it has
thus the type ``{\em list of anything}''. The list constructor
\verb|::| takes an element and a list (containing elements with the
same type) and returns a new list.  Here again, there is no type
constraint on the elements considered.

\begin{caml_example}
[];;
function head -> function tail -> head::tail;;
\end{caml_example}
We see here that the {\tt list} type is a {\it recursive} type.
The \verb"::" constructor receives two arguments; the second argument is itself a {\tt list}.

\section{Extracting elements from lists: pattern-matching}

We know how to build lists; we now show how to test emptiness of lists
(although  the equality predicate could be used for that) and extract
elements from lists (e.g. the first one).
We have used pattern-matching  on pairs, numbers or boolean
values. The syntax of patterns also includes list patterns. (We will
see that any data constructor can actually be used in a pattern.)
For lists, at least two cases have to be considered (empty, non empty):
\begin{caml_example}
let is_null = function [] -> true | _ -> false;;
let head = function x::_ -> x
                  | _ -> raise (Failure "head");;
let tail = function _::l -> l
                  | _ -> raise (Failure "tail");;
\end{caml_example}
The expression \verb|raise (Failure "head")| will produce a
run-time error when evaluated. In the definition of \verb"head" above,
we have chosen to forbid taking the head of an empty list. We could have
chosen \verb"tail []" to evaluate to \verb"[]", but we cannot return a value
for \verb"head []" without changing the type of the \verb"head" function.


We say that the types {\tt list} and {\tt bool} are {\it sum types},
because they are defined  with several alternatives:
\begin{itemize}
\item a list is either \verb"[]" or \verb"::" of \ldots
\item a boolean value is either \verb"true" or \verb"false"
\end{itemize}

Lists and booleans are typical examples of sum types. Sum types are
the only types whose values need run-time tests in order to be matched
by a non-variable pattern (i.e. a pattern that is not a single variable).


The cartesian product is a {\it product} type (only one alternative).
Product types do not involve run-time tests during pattern-matching,
because the type of their values suffices to indicate statically
what their structure is.

\section{Functions over lists}


We will see in this section the definition of some useful functions
over lists. These functions are of general interest, but are not
exhaustive. Some of them are predefined in the Caml Light system.
See also \cite{CAMLPrimer} or \cite{CAMLRefMan} for other examples of
functions over lists.

Computation of the length of a list:
\begin{caml_example}
let rec length = function [] -> 0
                        | _::l -> 1 + length(l);;
length [true; false];;
\end{caml_example}
Concatenating two lists:
\begin{caml_example}
let rec append =
     function [], l2 -> l2
            | (x::l1), l2 -> x::(append (l1,l2));;
\end{caml_example}
The \verb"append" function is already defined in Caml, and bound to the
infix identifier \verb"@".
\begin{caml_example}
[1;2] @ [3;4];;
\end{caml_example}
Reversing a list:
\begin{caml_example}
let rec rev = function [] -> []
                     | x::l -> (rev l) @ [x];;
rev [1;2;3];;
\end{caml_example}

The \verb"map" function applies a function on all the elements
of a list, and return the list of the function results. It
demonstrates full functionality (it takes a function
as argument), list processing, and polymorphism (note the sharing of type
variables between the arguments of \verb"map" in its type).
\begin{caml_example}
let rec map f =
    function [] -> []
           | x::l -> (f x)::(map f l);;
map (function x -> x+1) [1;2;3;4;5];;
map length [ [1;2;3]; [4;5]; [6]; [] ];;
\end{caml_example}



The following function is a list iterator. It takes a function $f$, a
base element $a$ and a list
\verb|[|$x_1$\verb|;|\ldots\verb|;|$x_n$\verb|]|. It computes:
$$
{\tt it\_list} ~ f ~ a ~ \verb|[|x_1\verb|;|\ldots\verb|;|x_n\verb|]| =
f~ (\ldots (f ~(f~ a~ x_1)~ x_2)~\ldots) x_n
$$
For instance, when applied to the curried addition function, to the
base element \verb"0", and to a list of numbers, it computes the sum
of all elements in the list. The expression:
\begin{itemize}
\item[] \verb|it_list (prefix +) 0 [1;2;3;4;5]|
\item[] evaluates to ((((0+1)+2)+3)+4)+5
\item[] i.e. to \verb|15|.
\end{itemize}
\begin{caml_example}
let rec it_list f a =
        function [] -> a
               | x::l -> it_list f (f a x) l;;
let sigma = it_list prefix + 0;;
sigma [1;2;3;4;5];;
it_list (prefix *) 1 [1;2;3;4;5];;
\end{caml_example}
The \verb|it_list| function is one of the many ways to iterate
over a list. For other list iteration functions, see \cite{CAMLPrimer}.

\section*{Exercises}

%
\begin{exo}\label{Lists:1}
Define the {\tt combine} function which, when given a pair of lists, returns a 
list of pairs such that:
\begin{verbatim}
combine ([x1;...;xn],[y1;...;yn]) = [(x1,y1);...;(xn,yn)]
\end{verbatim}
The function generates a run-time error if  the argument lists do not
have the same length.
\end{exo}
\begin{exo}\label{Lists:2}
Define a function which, when given a list, returns the list of all its 
sublists.
\end{exo}
