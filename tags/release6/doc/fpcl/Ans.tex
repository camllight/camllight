\chapter{Answers to exercises}\label{c:ans}
We give in this chapter one possible solution for each exercise
contained in this document. Exercises are referred to by their number
and the page where they have been proposed: for example, ``2.1, p.
15'' refers to the first exercise in chapter 2; this exercise is
located on page 15.

\newcommand{\exoref}[1]{\subsection*{\ref{#1}, p. \pageref{#1}}}

\exoref{Fund:1} The following (anonymous) functions have the required types:
\begin{enumerate}
\item
\begin{caml_example}
function f -> (f 2)+1;;
\end{caml_example}
\item
\begin{caml_example}
function m -> (function n -> n+m+1);;
\end{caml_example}
\item
\begin{caml_example}
(function f -> (function m -> f(m+1) / 2));;
\end{caml_example}
\end{enumerate}

\exoref{Fund:2} We must first rename {\tt y} to {\tt z}, obtaining:
\begin{verbatim}
(function x -> (function z -> x+z))
\end{verbatim}
and finally:
\begin{verbatim}
(function y -> (function z -> y+z))
\end{verbatim}
Without renaming, we would have obtained:
\begin{verbatim}
(function y -> (function y -> y+y))
\end{verbatim}
which does not denote the same function.

\exoref{Fund:3}
We write successively the reduction steps for each expressions, and
then we use Caml in order to check the result.
\begin{itemize}
\item
\begin{verbatim}
let x=1+2 in ((function y -> y+x) x);;
(function y -> y+(1+2)) (1+2);;
(function y -> y+(1+2)) 3;;
3+(1+2);;
3+3;;
6;;
\end{verbatim}
Caml says:
\begin{caml_example}
let x=1+2 in ((function y -> y+x) x);;
\end{caml_example}
\item
\begin{verbatim}
let x=1+2 in ((function x -> x+x) x);;
(function x -> x+x) (1+2);;
3+3;;
6;;
\end{verbatim}
Caml says:
\begin{caml_example}
let x=1+2 in ((function x -> x+x) x);;
\end{caml_example}
\item
\begin{verbatim}
let f1 = function f2 -> (function x -> f2 x)
in let g = function x -> x+1
   in f1 g 2;;
let g = function x -> x+1
in function f2 -> (function x -> f2 x) g 2;;
(function f2 -> (function x -> f2 x)) (function x -> x+1) 2;;
(function x -> (function x -> x+1) x)  2;;
(function x -> x+1) 2;;
2+1;;
3;;
\end{verbatim}
Caml says:
\begin{caml_example}
let f1 = function f2 -> (function x -> f2 x)
in let g = function x -> x+1
   in f1 g 2;;
\end{caml_example}
\end{itemize}

\exoref{Basic:1} To compute the surface area of a rectangle and the volume
of a sphere:
\begin{caml_example}
let surface_rect len wid = len * wid;;
let pi = 4.0 *. atan 1.0;;
let volume_sphere r = 4.0 /. 3.0 *. pi *. (power r 3.);;
\end{caml_example}

\exoref{Basic:2} In a call-by-value language without conditional construct
(and without sum types), all programs involving a recursive definition
never terminate.

\exoref{Basic:3}
\begin{caml_example}
let rec factorial n = if n=1 then 1 else n*(factorial(n-1));;
factorial 5;;
let tail_recursive_factorial n =
  let rec fact n m = if n=1 then m else fact (n-1) (n*m)
  in fact n 1;;
tail_recursive_factorial 5;;
\end{caml_example}

\exoref{Basic:5}
\begin{caml_example}
let rec fibonacci n =
  if n=1 then 1
         else if n=2 then 1
                     else fibonacci(n-1) + fibonacci(n-2);;
fibonacci 20;;
\end{caml_example}

\exoref{Basic:6}
\begin{caml_example}
let compose f g = function x -> f (g (x));;
let curry f = function x -> (function y -> f(x,y));;
let uncurry f = function (x,y) -> f x y;;
uncurry compose;;
compose curry uncurry;;
compose uncurry curry;;
\end{caml_example}

\exoref{Lists:1}
\begin{caml_example}
let rec combine =
  function [],[] -> []
         | (x1::l1),(x2::l2) -> (x1,x2)::(combine(l1,l2))
         | _ -> raise (Failure "combine: lists of different length");;
combine ([1;2;3],["a";"b";"c"]);;
combine ([1;2;3],["a";"b"]);;
\end{caml_example}

\exoref{Lists:2}
\begin{caml_example}
let rec sublists =
    function [] -> [[]]
           | x::l -> let sl = sublists l
                     in sl @ (map (fun l -> x::l) sl);;
sublists [];;
sublists [1;2;3];;
sublists ["a"];;
\end{caml_example}

\exoref{Types:1}
\begin{caml_example}
type ('a,'b) btree = Leaf of 'b
                   | Btree of ('a,'b) node
and ('a,'b) node = {Op:'a;
                    Son1: ('a,'b) btree;
                    Son2: ('a,'b) btree};;
let rec nodes_and_leaves =
    function Leaf x -> ([],[x])
           | Btree {Op=x; Son1=s1; Son2=s2} ->
                let (nodes1,leaves1) = nodes_and_leaves s1
                and (nodes2,leaves2) = nodes_and_leaves s2
                in (x::nodes1@nodes2, leaves1@leaves2);;
nodes_and_leaves (Btree {Op="+"; Son1=Leaf 1; Son2=Leaf 2});;
\end{caml_example}

\exoref{Types:2}
\begin{caml_example}
let rec map_btree f g = function
      Leaf x -> Leaf (f x)
    | Btree {Op=op; Son1=s1; Son2=s2}
               -> Btree {Op=g op; Son1=map_btree f g s1;
                                  Son2=map_btree f g s2};;
\end{caml_example}

\exoref{Types:3}
We need to give a functional interpretation to {\tt btree} data constructors.
We use {\tt f} (resp. {\tt g}) to denote the function associated to
the {\tt Leaf} (resp. {\tt Btree}) data constructor, obtaining the
following Caml definition:
\begin{caml_example}
let rec btree_it f g = function
      Leaf x -> f x
    | Btree{Op=op; Son1=s1; Son2=s2}
              -> g op (btree_it f g s1) (btree_it f g s2)
;;
btree_it (function x -> x)
         (function "+" -> prefix +
                 | _ -> raise (Failure "Unknown op"))
         (Btree {Op="+"; Son1=Leaf 1; Son2=Leaf 2});;
\end{caml_example}

\exoref{Annot:4}
\begin{caml_example}
type ('a,'b) lisp_cons = {mutable Car:'a; mutable Cdr:'b};;
let car p = p.Car
and cdr p = p.Cdr
and rplaca p v = p.Car <- v
and rplacd p v = p.Cdr <- v;;
let p = {Car=1; Cdr=true};;
rplaca p 2;;
p;;
\end{caml_example}

\exoref{Annot:5}
\begin{caml_example}
let stamp_counter = ref 0;;
let stamp () =
  stamp_counter := 1 + !stamp_counter; !stamp_counter;;
stamp();;
stamp();;
\end{caml_example}

\exoref{Annot:6}
\begin{caml_example}
let exchange t i j =
  let v = t.(i) in vect_assign t i t.(j); vect_assign t j v
;;
let quick_sort t =
  let rec quick lo hi =
    if lo < hi
    then begin
          let i = ref lo
          and j = ref hi
          and p = t.(hi) in
          while !i < !j
           do
            while !i < hi & t.(!i) <=. p do incr i done;
            while !j > lo & p <=. t.(!j) do decr j done;
            if !i < !j then exchange t !i !j
           done;
          exchange t hi !i;
          quick lo (!i - 1);
          quick (!i + 1) hi
         end
     else ()
  in quick 0 (vect_length t - 1)
;;
let a = [| 2.0; 1.5; 4.0; 0.0; 10.0; 1.0 |];;
quick_sort a;;
a;;
\end{caml_example}

\exoref{Exc:1}
\begin{caml_example}
let rec find_succeed f = function
      [] -> raise (Failure "find_succeed")
    | x::l -> try f x; x with _ -> find_succeed f l
;;
let hd = function [] -> raise (Failure "empty") | x::l -> x;;
find_succeed hd [[];[];[1;2];[3;4]];;
\end{caml_example}

\exoref{Exc:2}
\begin{caml_example}
let rec map_succeed f = function
      [] -> []
    | h::t -> try (f h)::(map_succeed f t) 
              with _ ->  map_succeed f t;;
map_succeed hd [[];[1];[2;3];[4;5;6]];;
\end{caml_example}

\exoref{IO:1}
The first function ({\tt copy}) that we define assumes that its
arguments are respectively the input and the output channel. They are
assumed to be already opened.
\begin{caml_example}
let copy inch outch =
  (* inch and outch are supposed to be opened channels *)
  try (* actual copying *)
      while true
      do output_char outch (input_char inch)
      done
   with End_of_file -> (* Normal termination *)
             raise End_of_file
      | sys__Sys_error msg -> (* Abnormal termination *)
             prerr_endline msg;
             raise (Failure "cp")
      | _ -> (* Unknow exception, maybe interruption? *)
             prerr_endline "Unknown error while copying";
             raise (Failure "cp")
;;
\end{caml_example}
The next function opens channels connected to its filename arguments,
and calls {\tt copy} on these channels. The advantage of dividing the
code into two functions is that {\tt copy} performs the actual work,
and can be reused in different applications, while the role of {\tt
cp} is more ``administrative'' in the sense that it does nothing but
opening and closing channels and printing possible error messages.
\begin{caml_example}
let cp f1 f2 =
  (* Opening channels, f1 first, then f2 *)
  let inch =
      try open_in f1
      with sys__Sys_error msg -> 
                prerr_endline (f1^": "^msg);
                raise (Failure "cp")
         | _ -> prerr_endline ("Unknown exception while opening "^f1);
                raise (Failure "cp")
  in
  let outch =
      try open_out f2
      with sys__Sys_error msg -> 
                close_in inch;
                prerr_endline (f2^": "^msg);
                raise (Failure "cp")
         | _ -> close_in inch;
                prerr_endline ("Unknown exception while opening "^f2);
                raise (Failure "cp")
  in (* Copying and then closing *)
     try copy inch outch
     with End_of_file -> close_in inch; close_out outch
                        (* close_out flushes *)
        | exc -> close_in inch; close_out outch; raise exc
;;
\end{caml_example}
Let us try {\tt cp}:
\begin{caml_eval}
try sys__remove "/tmp/foo" with _ -> ();;
\end{caml_eval}
\begin{caml_example}
cp "/etc/passwd" "/tmp/foo";;
cp "/tmp/foo" "/foo";;
\end{caml_example}
The last example failed because a regular user is not allowed to write
at the root of the file system.
\begin{caml_eval}
try sys__remove "/tmp/foo" with _ -> ();;
\end{caml_eval}

\exoref{IO:2}
As in the previous exercise, the function {\tt count} performs the
actual counting. It works on an input channel and returns a pair of integers.
\begin{caml_example}
let count inch =
    let chars = ref 0
    and lines = ref 0 in
    try
      while true do
        let c = input_char inch in
          chars := !chars + 1;
          if c = `\n` then lines := !lines + 1 else ()
      done;
      (!chars, !lines)
    with End_of_file -> (!chars, !lines)
;;
\end{caml_example}
The function {\tt wc} opens a channel on its filename argument, calls
{\tt count} and prints the result.
\begin{caml_example}
let wc f =
    let inch =
       try open_in f
       with sys__Sys_error msg -> 
                prerr_endline (f^": "^msg);
                raise (Failure "wc")
         | _ -> prerr_endline ("Unknown exception while opening "^f);
                raise (Failure "wc")
  in let (chars,lines) = count inch
     in   print_int chars;
          print_string " characters, ";
          print_int lines;
          print_string " lines.\n"
;;
\end{caml_example}
Counting {\tt /etc/passwd} gives:
\begin{caml_example}
wc "/etc/passwd";;
\end{caml_example}

\exoref{Streams:1}
Let us recall the definitions of the type {\tt token} and of the
lexical analyzer:
\begin{caml_example}
type token =
  PLUS | MINUS | TIMES | DIV | LPAR | RPAR
| INT of int;;
(* Spaces *)
let rec spaces = function
  [< '` `|`\t`|`\n`; spaces _ >] -> ()
| [< >] -> ();;
(* Integers *)
let int_of_digit = function
  `0`..`9` as c -> (int_of_char c) - (int_of_char `0`)
| _ -> raise (Failure "not a digit");;
let rec integer n = function
  [< ' `0`..`9` as c; (integer (10*n + int_of_digit c)) r >] -> r
| [< >] -> n;;
(* The lexical analyzer *)
let rec lexer s = match s with
  [< '`(`; spaces _ >] -> [< 'LPAR; lexer s >]
| [< '`)`; spaces _ >] -> [< 'RPAR; lexer s >]
| [< '`+`; spaces _ >] -> [< 'PLUS; lexer s >]
| [< '`-`; spaces _ >] -> [< 'MINUS; lexer s >]
| [< '`*`; spaces _ >] -> [< 'TIMES; lexer s >]
| [< '`/`; spaces _ >] -> [< 'DIV; lexer s >]
| [< '`0`..`9` as c; (integer (int_of_digit c)) n; spaces _ >]
                                -> [< 'INT n; lexer s >];;
\end{caml_example}
The parser has the same shape as the grammar:
\begin{caml_example}
let rec expr = function
  [< 'INT n >] -> n
| [< 'PLUS; expr e1; expr e2 >] -> e1+e2
| [< 'MINUS; expr e1; expr e2 >] -> e1-e2
| [< 'TIMES; expr e1; expr e2 >] -> e1*e2
| [< 'DIV; expr e1; expr e2 >] -> e1/e2;;
expr (lexer (stream_of_string "1"));;
expr (lexer (stream_of_string "+ 1 * 2 4"));;
\end{caml_example}

\exoref{Streams:2}
The only new function that we need is a function taking as argument a
character stream, and returning the first identifier of that stream.
It could be written as:
\begin{caml_example}
let ident_buf = make_string 8 ` `;;
let rec ident len = function
  [< ' `a`..`z`|`A`..`Z` as c;
     (if len >= 8 then ident len
      else begin
            set_nth_char ident_buf len c;
            ident (succ len)
           end) s >] -> s
| [< >] -> sub_string ident_buf 0 len;;
\end{caml_example}
The lexical analyzer will first try to recognize an alphabetic
character $c$, then put $c$ at position 0 of \verb|ident_buf|, and
call {\tt ident 1} on the rest of the character stream.  Alphabetic
characters encountered will be stored in the string buffer
\verb|ident_buf|, up to the 8th. Further alphabetic characters will be
skipped. Finally, a substring of the buffer will be returned as
result.
\begin{caml_example}
let s = stream_of_string "toto 1";;
ident 0 s;;
(* Let us see what remains in the stream *)
match s with [< 'c >] -> c;;
let s = stream_of_string "LongIdentifier ";;
ident 0 s;;
match s with [< 'c >] -> c;;
\end{caml_example}

The definitions of the new {\tt token} type and of the lexical analyzer
is trivial, and we shall omit them. A slightly more complex lexical
analyzer recognizing identifiers (lowercase only) is given in
section~\ref{s:ASLlexing} in this part.

\exoref{Modules:1}
\begin{verbatim}
(* main.ml *)
let chars = counter__new 0;;
let lines = counter__new 0;;

let count_file filename =
  let in_chan = open_in filename in
    try
      while true do
        let c = input_char in_chan in
          counter__incr chars;
          if c = `\n` then counter__incr lines
      done
    with End_of_file ->
      close_in in_chan
;;

for i = 1 to vect_length sys__command_line - 1 do
  count_file sys__command_line.(i)
done;
print_int (counter__read chars);
print_string " characters, ";
print_int (counter__read lines);
print_string " lines.\n";
exit 0;;
\end{verbatim}

