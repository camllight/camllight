\chapter{Escaping from computations: exceptions}
\label{c:exceptions}

In some situations, it is necessary to abort computations.
If we are
trying to compute the integer division of an integer {\tt n} by {\tt 0},
then we must escape from that embarrassing situation without returning any
result.

Another example of the usage of such an escape mechanism appears when we
want to define the {\tt head} function on lists:%
\begin{caml_example}
let head = function
     x::L -> x
   | [] -> raise (Failure "head: empty list");;
\end{caml_example}
We cannot give a regular value to the expression {\tt head []} without
losing the polymorphism of {\tt head}. We thus choose to escape: we {\em
raise an exception}.

\section{Exceptions}

An exception is a Caml value of the built-in type \verb"exn", very similar to a sum type. An exception:
\begin{itemize}
\item has a {\em name} ({\tt Failure} in our example),
\item and holds zero or one value ({\tt "head: empty list"} of type {\tt string} in the example).
\end{itemize}

Some exceptions are predefined, like {\tt Failure}. New 
exceptions can be defined with the following construct:
\ikwd{exception@\verb`exception`}
\begin{verbatim}
exception <exception name> [of <type>]
\end{verbatim}

Example:%
\begin{caml_example}
exception Found of int;;
\end{caml_example}
The exception {\tt Found} has been declared, and it carries integer
values. When we apply it to an integer, we get an exception value
(of type \verb"exn"):
\begin{caml_example}
Found 5;;
\end{caml_example}

\section{Raising an exception}

Raising an exception is done by applying the primitive function
\verb"raise" to a value of type \verb"exn":
\begin{caml_example}
raise;;
raise (Found 5);;
\end{caml_example}
Here is a more realistic example:
\begin{caml_example}
let find_index p =
  let rec find n =
    function [] -> raise (Failure "not found")
           | x::L -> if p(x) then raise (Found n)
                     else find (n+1) L
  in find 1;;
\end{caml_example}
The \verb"find_index" function always fails. It raises:
\begin{itemize}
\item {\tt Found n}, if there is an element {\tt x} of the list such that
      {\tt p(x)}, in this case {\tt n} is the index of {\tt x} in the list,
\item the {\tt Failure} exception if no such {\tt x} has been found.
\end{itemize}
Raising exceptions is more than an error mechanism: it is a programmable
control structure.
In the \verb"find_index" example, there was no error when raising the {\tt Found} exception: we
only wanted to quickly escape from the computation, since we found what we
were looking for.
This is why it must be possible to {\em trap} exceptions:
we want to trap possible errors, but we also want to get our result in the
case of the \verb"find_index function".

\section{Trapping exceptions}

Trapping exceptions is achieved by the following construct:
\ikwd{try@\verb`try`}
\begin{verbatim}
try <expression> with <match cases>
\end{verbatim}

This construct evaluates
\verb"<expression>". If no exception is raised during the evaluation, then
the result of the {\tt try} construct is the result of \verb"<expression>".
If an exception is raised during this evaluation, then the raised exception
is matched against the \verb"<match cases>". If a case matches, then control
is passed to it. If no case matches, then the exception is propagated
outside of the {\tt try} construct, looking for the enclosing {\tt try}.%

Example:
\begin{caml_example}
let find_index p L =
  let rec find n =
    function [] -> raise (Failure "not found")
           | x::L -> if p(x) then raise (Found n)
                     else find (n+1) L
  in
    try find 1 L with Found n -> n;;
find_index (function n -> (n mod 2) = 0) [1;3;5;7;9;10];;
find_index (function n -> (n mod 2) = 0) [1;3;5;7;9];;
\end{caml_example}
The \verb"<match cases>" part of the {\tt try} construct is a regular pattern matching on values of type \verb"exn".
It is thus possible to trap any exception
by using the \verb"_" symbol.
As an example, the following function traps any
exception raised during the application of its two arguments. Warning: the
\verb"_" will also trap interrupts from the keyboard such as control-C!
\begin{caml_example}
let catch_all f arg default =
       try f(arg) with _ -> default;;
\end{caml_example}
It is even possible to catch all exceptions, do something special (close or remove opened files, for example), and raise again that exception, to propagate it upwards.
\begin{caml_example}
let show_exceptions f arg =
        try f(arg) with x -> print_string "Exception raised!\n"; raise x;;
\end{caml_example}
In the example above, we print a message to the
standard output channel (the terminal), before raising again the trapped
exception.
\begin{caml_example}
catch_all (function x -> raise (Failure "foo")) 1 0;;
catch_all (show_exceptions (function x -> raise (Failure "foo"))) 1 0;;
\end{caml_example}

\section{Polymorphism and exceptions}

Exceptions must not be polymorphic for a reason similar to the one for
references (although it is a bit harder to give an example).
\begin{caml_example}
exception Exc of 'a list;;
\end{caml_example}
One reason is that the {\tt excn} type is not a parameterized type,
but one deeper reason is that if the exception {\tt Exc} is declared
to be polymorphic, then a function may raise {\tt Exc [1;2]}. There
might be no mention of that fact in the type inferred for the
function.  Then, another function may trap that exception, obtaining
the value {\tt [1;2]} whose real type is {\tt int list}.  But the only
type known by the typechecker is {\tt 'a list}: the {\tt try} form
should refer to the {\tt Exc} data constructor, which is known to be
polymorphic.  It may then be possible to build an ill-typed Caml value
{\tt [true; 1; 2]}, since the typechecker does not possess any further
type information than {\tt 'a list}.

The problem is thus the absence of static connection from exceptions
that are raised and the occurrences where they are trapped. Another
example would be the one of a function raising {\tt Exc} with an
integer or a boolean value, depending on some condition. Then, in that
case, when trying to trap these exceptions, we cannot decide wether
they will hold integers or boolean values.


\section*{Exercises}

\begin{exo}\label{Exc:1}
Define the function \verb"find_succeed" which given a function {\tt f} and a
list {\tt L} returns the first element of {\tt L} on which the application
of {\tt f} succeeds.
\end{exo}
\begin{exo}\label{Exc:2}
Define the function \verb"map_succeed" which given a function {\tt f} and a
list {\tt L} returns the list of the results of successful applications of
{\tt f} to elements of {\tt L}.
\end{exo}
