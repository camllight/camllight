\chapter*{BNF syntax of the interface description language}
\begin{verbatim}
Type0 :
      TYINT
    | TYFLOAT
    | TYBOOL
    | TYCHAR
    | TYSTRING
    | WIDGET
    | IDENT

Type01 :
       Type0
     | IDENT LPAREN IDENT RPAREN
     | WIDGET LPAREN IDENT RPAREN
     | OPTION LPAREN IDENT RPAREN

Type1 :
      Type01
    | Type01 LIST

Type1list :
      Type1 COMMA Type1list
    | Type1

Typearg :
      LPAREN RPAREN
    | LPAREN Type1 RPAREN
    | LPAREN Type1list RPAREN

Type :
     Typearg
   | LPAREN FUNCTION Typearg RPAREN

Constructor :
     IDENT STRING
   | IDENT Type
   | IDENT STRING Type

AbbrevConstructor :
     Constructor
   | IDENT

Constructors :
     Constructor Constructors
   | Constructor

AbbrevConstructors :
     AbbrevConstructor AbbrevConstructors
   | AbbrevConstructor

Command :
     FUNCTION Typearg IDENT STRING Type

Option :
     OPTION IDENT STRING Type
   | OPTION IDENT

WidgetComponent :
     Command
   | Option

WidgetComponents :
     /* epsilon */
   | WidgetComponent WidgetComponents

ModuleComponents :
     /* epsilon */
   | Command ModuleComponents

Entry :
     WIDGET IDENT LBRACE WidgetComponents RBRACE
   | Command
   | TYPE IDENT LBRACE Constructors RBRACE
   | TYPE IDENT EXTERNAL
   | SUBTYPE OPTION LPAREN IDENT RPAREN LBRACE AbbrevConstructors RBRACE
   | SUBTYPE IDENT LPAREN IDENT RPAREN LBRACE AbbrevConstructors RBRACE  
   | MODULE IDENT LBRACE ModuleComponents RBRACE
   | EOF
\end{verbatim} 
